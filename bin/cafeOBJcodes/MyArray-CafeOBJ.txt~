mod* MyArray  {

*[ma]*
pr(INT)
pr(EQL)
pr(BOOL)

-- observers
bop getElementAt : ma Int -> NzInt
bop getSize : ma -> Int

-- transitions
bop setSize : ma Int -> ma
bop setElementAt : ma Int Int -> ma 

-- variables
var A : ma
vars I V J : Int

-- constant denoting the initial state of the array
op initMa : -> ma

-- initially
eq getElementAt(initMa,I) = -1 . 
eq getSize(initMa) = 0 .

-- setSize

op c-setSize : Int ma -> Bool
eq c-setSize(J,A) = (J >= 0) .

ceq getSize(setSize(A,J)) = J if c-setSize(J,A) .
ceq getElementAt(setSize(A,J),I) = getElementAt(A,I) if c-setSize(J,A) .

bceq setSize(A,J) = A if not c-setSize(J,A) .


-- setElementAt

op c-setElementAt : ma Int Int -> Bool
eq c-setElementAt(A,I,V) = (I >= 0) and (I < getSize(A)) and (V >= 0) .

ceq getSize(setElementAt(A,I,V)) = getSize(A) if c-setElementAt(A,I,V) .
ceq getElementAt(setElementAt(A,I,V),J) = V if c-setElementAt(A,I,V) and (I = J) . 
ceq getElementAt(setElementAt(A,I,V),J) = getElementAt(A,J) if c-setElementAt(A,I,V) and not (I = J) . 


bceq setElementAt(A,I,V) = A if not c-setElementAt(A,I,V) .


}
